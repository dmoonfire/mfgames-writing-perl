#!/usr/bin/perl

#
# Setup
#

# Directives
use strict;
use warnings;
use utf8;
use feature 'unicode_strings';

binmode(STDERR, ":utf8");
binmode(STDOUT, ":utf8");
binmode(STDIN, ":utf8");

# Modules
use Cwd qw(abs_path);
use Encode qw(encode_utf8 decode_utf8);
use File::Basename;
use Getopt::Long;
use IO::Handle;
use IO::Pty;
use IPC::Open2;
use List::Util qw(first);

Getopt::Long::Configure("pass_through");

# Parameters
my $ASPELL = "/usr/bin/aspell";
my $LOCAL_DIR;
my $LOG_CONSOLE = 0;
my $LOG_FILE;
my $REWRITE = 0;
my $EMACS = 0;
my $IS_LOADED = 0;

&GetOptions(
	"--rewrite!" => \$REWRITE,
	"--emacs!" => \$EMACS,
	"--verbose!" => \$LOG_CONSOLE,
	"--log=s" => \$LOG_FILE,
	# We grab these here to strip it off the aspell's command line.
	"-p=s" => \$LOCAL_DIR);

# Dictionary
my @comments = ();
my @directives = ();

my %dictionary = ();
my %dictionary_include = ();
my %replacements = ();
my %replacements_include = ();
my %suggestions = ();
my %suggestions_include = ();
my @suggestion_maps = ();

#
# Command Line
#

# Check to see if we have a rewrite or generation option.
load_dictionary();
save_dictionary() if $REWRITE;
write_emacs_abbrev() if $EMACS;

exit 0 if $REWRITE || $EMACS;

# Automatically flush the output.
$|++;

# See if we are in pipe mode, which is the "-a" flag.
my $a_index = first { $ARGV[$_] eq "-a" } 0..$#ARGV;

if (defined $a_index)
{
	# Load the dictionary into memory. We have "auto-revert" just to
	# make it easier to debug.
	log_line("Pipe: ", join(" ", @ARGV));

	# We may need to modify the arguments to `aspell`.
	my @aspell_commands = @ARGV;

	# Create the bidirectional pipe to `aspell`.
	my $reader = new IO::Pty;
	my $writer = new IO::Pty;

	my $pid = open2(
		$reader,
		$writer,
		"$ASPELL " . join(" ", @aspell_commands));

	binmode($reader, ":utf8");
	binmode($writer, ":utf8");

	# Make sure whatever we write, we flush out.
	$writer->autoflush(1);

	# Grab the first line which is always the version.
	my $got = $reader->getline;
	print $got;

	# Now process through the standard input.
	my $input = " ";

	while ($input ne "")
	{
		# Get the input from the pipe.
		$input = <STDIN>;
		last unless defined $input;
		chomp($input);

		# If the command starts with a "&", then we are adding a
		# lowercase version of the word. We basically modify it and
		# replace it with a "*" command which is add word.
		if (defined $LOCAL_DIR && $input =~ s@^\&@@)
		{
			$input = "*" . lc($input);
		}

		# If we start with a "*", then we need to add the word to the
		# dictionary. We add this to our internal dictionary instead
		# of aspell's personal dictionary.
		if (defined $LOCAL_DIR && $input =~ s@^\*@@)
		{
			# Make sure our dictionary is loaded.
			log_line("add:", $input);

			unless (exists $dictionary{input})
			{
				$dictionary{$input} = 1;
				save_dictionary();
			}
			
			# We don't need to do anything.
			next;
		}

		# Pass it into `aspell`.
		log_line("in: ", $input);
		$writer->print("$input\n");

		# Ignore the non-output lines.
		next if $input =~ m/^[\@\!\-\%\#\*\&\$]/;

		# For everything else, just pass it over and parse the
		# results. We have a blank line to indicate we are done
		# answering.

		# Read the input from the pipe.
		my $index = 0;
		my $is_search = 0;
		$is_search = 1 if $input=~ m@^\^@;
		my $sent_response;

		while (1)
		{
			# Read in the line from the pipe.
			$index++;
			$got = $reader->getline;

			# See if we are processing a suggestion.
			if ($got =~ m@^\& (.*?) (\d+) (\d+): (.*?)$@s)
			{
				# See if we know about the word.
				my $search = $1;
				my $number = $2;
				my $index = $3;
				my $suggestions = $4;
				my @suggestions = split(/, /, $suggestions);

				# Check to see if this is a valid word.
				if (check_word($search))
				{
					# This is a valid word, so skip it.
					next;
				}

				# Get some additional suggestions.
				if (check_suggestion($search, $number, $index, \@suggestions))
				{
					$sent_response = 1;
					next;
				}

				# We don't, so pass it out.
				$sent_response = 1;
			}

			# If we are done, then see if we sent at least one response.
			print "*\n" if (!$sent_response && $got =~ m@^\s*$@s);

			# Write out to the system.
			log_line("out:", $got);
			print "$got";

			# If we have a blank line.
			last if ($got =~ m@^\s*$@s);
		}
	}
	
	# We're done with pipe processing.
	exit 0;
}

# For everything else, just pass it on.
log_line("Pass: ", join(" ", @ARGV));
system($ASPELL, @ARGV);
my $return_code = $? >> 8;
exit $return_code;

#
# Dictionary Processing
#

sub load_dictionary
{
	# If we don't have a dictionary, skip it.
	return unless defined $LOCAL_DIR;
	return if $IS_LOADED;
	$IS_LOADED = 1;

	# Load the dictionary file.
	my $local_dict = "$LOCAL_DIR/local.words";

	load_dictionary_file(
		$LOCAL_DIR,
		$local_dict,
		\%dictionary,
		\@directives,
		\@comments,
		\%suggestions,
		\%replacements);
}

sub load_dictionary_file
{
	# Pull out the arguments.
	my ($dir, $file, $dict, $cmds, $cmts, $suggests, $replaces) = @_;
	my ($command_count, $word_count, $suggest_count, $replace_count) =
		(0, 0, 0, 0);

	# Figure out the filename. If we don't have it, we don't have
	# anything to do.
	log_line("Using dictionary: $file");

	return unless -f $file;

	# Load the dictionary into memory.
	my $dfh;

	open $dfh, "<", $file
		or die "Cannot read $file ($!@)";

	binmode($dfh, ":utf8");

	while (<$dfh>)
	{
		# Ignore blank lines.
		next if m@^\s*$@;

		# Clean up the line and pull out the operation.
		chomp;
		
		die "Cannot parse line $.: $_"
			unless m@^(\#|\w+)\s+(.*?)$@;

		my $line = $_;
		my $operation = $1;
		my $argument = $2;

		# Figure out what to do based on the operation.
		if ($operation eq "word")
		{
			# Add the word to the dictionary.
			$$dict{$argument} = 1;
			$word_count++;

			# If we have suggestion map, process it.
			if (@suggestion_maps)
			{
				my $suggest = $argument;

				foreach my $map (@suggestion_maps)
				{
					eval('$suggest =~ ' . $map);
				}

				# If we are different, then add it.
				if ($suggest ne $argument)
				{
					# We always add to the included list.
					unless (exists $suggestions_include{$suggest})
					{
						my @empty = ();
						$suggestions_include{$suggest} = \@empty;
					}
					
					log_line("Adding $argument to $suggest");
					push $suggestions_include{$suggest}, $argument;
				}
			}
		}
		elsif ($operation eq "command")
		{
			push @$cmds, $argument;
			process_command($LOCAL_DIR, $argument);
			$command_count++;
		}
		elsif ($operation eq "#")
		{
			push @$cmts, $argument;
		}
		elsif ($operation eq "suggest")
		{
			die "Cannot parse line $.: Unknown suggestion: $line"
				unless $argument =~ m@^([^\s\=]+)\s*=\s*(.*?)\s*$@;
			my @words = split(/,\s*/, $2);
			$$suggests{$1} = \@words;
			$suggest_count++;
		}
		elsif ($operation eq "replace")
		{
			die "Cannot parse line $.: Unknown replacement: $line"
				unless $argument =~ m@^([^\s\=]+) = (.*?)$@;
			$$replaces{$1} = $2;
			$replace_count++;
		}
	}

	# Close the handle.
	close $dfh;

	# Write out information.
	log_line(
		sprintf(
			"$file: %d commands, %d words, %d suggests, %d replaces",
			$command_count,
			$word_count,
			$suggest_count,
			$replace_count));
}

sub save_dictionary
{
	# If we don't have a dictionary, skip it.
	return unless defined $LOCAL_DIR;

	# Figure out the filename.
	my $local_dict = "$LOCAL_DIR/local.words";

	# Write out the dictionary file.
	my $dfh;
	open $dfh, ">", $local_dict
		or die "Cannot write $local_dict ($!@)";

	binmode($dfh, ":utf8");

	# Start with comments.
	if (@comments)
	{
		print $dfh "# ", join("\n# ", @comments), "\n\n";
	}

	# Add in the directives.
	if (@directives)
	{
		print $dfh "command ", join("\ncommand ", @directives), "\n\n";
	}

	# Add in the suggestions.
	if (%suggestions)
	{
		foreach my $s (sort keys %suggestions)
		{
			my $sw = join(", ", @{$suggestions{$s}});
			print $dfh "suggest $s = $sw\n";
		}

		print $dfh "\n";
	}

	# Write out the replacements.
	if (%replacements)
	{
		foreach my $s (sort keys %replacements)
		{
			my $sw = $replacements{$s};
			print $dfh "replace $s = $sw\n";
		}

		print $dfh "\n";
	}

	# Write out the words in the dictionary.
	foreach my $key (sort keys %dictionary)
	{
		print $dfh "word    $key\n";
	}

	# Finish up the dictionary.
	close $dfh;
}

#
# Commands
#

sub process_command
{
	# Parse the command.
	my ($dict, $line) = @_;
	
	die "Cannot parse command: $line"
		unless $line =~ m@^((?:map )?\w+)\s*(.*?)$@;

	my $command = $1;
	my $argument = $2;

	# Figure out the operation.
	if ($command eq "include")
	{
		# Figure out the relative path.
		die "Cannot parse include: $argument"
			unless $argument =~ m@\"(.*?)\"@;

		my $rel_filename = $1;
		my $dict_file = abs_path("$dict/$1");
		my $dict_dir = dirname($dict_file);

		# Load the file, stripping off directives and comments.
		my @directives_ignore = ();
		my @comments_ignore = ();

		load_dictionary_file(
			$dict_dir,
			$dict_file,
			\%dictionary_include,
			\@directives_ignore,
			\@comments_ignore,
			\%suggestions_include,
			\%replacements_include);
	}
	elsif ($command eq "log")
	{
		# Figure out the relative path.
		die "Cannot parse log: $argument"
			unless $argument =~ m@\"(.*?)\"@;

		my $rel_filename = $1;
		my $log_file = abs_path("$dict/$1");

		# Change the log file.
		$LOG_FILE = $log_file;

		log_line("Log: $log_file");
	}
	elsif ($command eq "map suggest")
	{
		push @suggestion_maps, $argument;
		log_line("command map suggest $argument");
	}
	else
	{
		die "Unknown command: $command";
	}
}

#
# Suggestions
#

sub check_word
{
	# Check to see if it is a known word first in either the
	# dictionary or the dictionary_include dictionaries.
	my $search = shift @_;
	my $lc_search = lc($search);

	# Search the dictionary.
	my $s_d = exists $dictionary{$search};
	my $l_d = exists $dictionary{$lc_search};
	my $s_i = exists $dictionary_include{$search};
	my $l_i = exists $dictionary_include{$lc_search};

	log_line("Check $search/$lc_search $s_d $l_d $s_i $l_i");
	
	if ($s_d || $l_d || $s_i || $l_i)
	{
		log_line("Found: $search");
		return 1;
	}

	return 0;
}

sub	check_suggestion
{
	# Pull out the parameters.
	my ($search, $number, $index, $suggestions) = @_;
	my $lc_search = lc($search);

	# Check to see if we have a suggestion for this one.
	my @suggestions = @$suggestions;
	unshift @suggestions, get_suggestions($lc_search, \%suggestions_include);
	unshift @suggestions, get_suggestions($search, \%suggestions_include);
	unshift @suggestions, get_suggestions($lc_search, \%suggestions);
	unshift @suggestions, get_suggestions($search, \%suggestions);

	# Remove any duplicates, starting at the beginning.
	my %seen = ();
	my @results = ();

	map { unless (exists $seen{$_}) { $seen{$_} = 1; push @results, $_ } }
	@suggestions;

	# Write out the suggestions.
	print("& $search "
		  . scalar(@results)
		  . " $index: "
		  . join(", ", @results)
		  . "\n");

	# Write out the results.
	return 1;
}

sub get_suggestions
{
	my ($search, $ref) = @_;

	if (exists $$ref{$search})
	{
		my $suggestion_ref = $$ref{$search};
		my @suggestion = @$suggestion_ref;
		return @suggestion;
	}

	# Return an empty list.
	return ();
}

#
# Emacs
#

sub write_emacs_abbrev
{
	# Write out an emacs abbreviation file in the same directory.
	open EMACS, ">", "$LOCAL_DIR/abbrev.el"
		or die "Cannot write $LOCAL_DIR/abbrev.el ($!)";

	# Write out the header.
	print EMACS "(define-abbrev-table 'text-mode-abbrev-table\n";
	print EMACS "  '(\n";

	# Write out all the replacements.
	foreach my $key (sort keys %replacements)
	{
		my $value = $replacements{$key};
		print EMACS "    (\"$key\" \"$value\")\n";
	}

	foreach my $key (sort keys %replacements_include)
	{
		my $value = $replacements_include{$key};
		print EMACS "    (\"$key\" \"$value\")\n";
	}

	# Finish up the file.
	print EMACS "    ))\n";
	close EMACS;
}

#
# Logging
#

sub log_line
{
	my $message = join(" ", @_);
	chomp($message);

	# Log to the console.
	print STDERR "LOG: $message\n"
		if $LOG_CONSOLE;

	# Log to the file, if requested.
	if (defined $LOG_FILE)
	{
		open LOG, ">>", $LOG_FILE
			or die "Cannot write $LOG_FILE ($!)";
		binmode(LOG, ":utf8");
		print LOG "$message\n";
		close LOG;
	}
}
